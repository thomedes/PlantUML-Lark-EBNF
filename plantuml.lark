start: _diagram

WS: " " | "\t" | "\n"
%ignore WS

_eol: "\n"

_diagram: ( _uml_diagram
         | salt_diagram
         | gantt_diagram
         | mindmap_diagram
         | wbs_diagram )

// ----------------------------------------------------------------------------
//  TODO All this needs to be done
// ----------------------------------------------------------------------------

salt_diagram:    "@startsalt"    diagram_name? _eol wireframe_diagram "@endsalt"
gantt_diagram:   "@startgantt"   diagram_name? _eol "FIXME" "@endgantt"
mindmap_diagram: "@startmindmap" diagram_name? _eol "FIXME" "@endmindmap"
wbs_diagram:     "@startwbs"     diagram_name? _eol "FIXME" "@endwbs"

// The diagram name before FIXME only used to avoid collisions
sequence_diagram:   "sequence      FIXME"
usecase_diagram:    "usecase       FIXME"
class_diagram:      "class         FIXME"
activity_diagram:   "activity      FIXME"
component_diagram:  "component     FIXME"
object_diagram:     "object        FIXME"
deployment_diagram: "deployment    FIXME"
timing_diagram:     "timing        FIXME"
network_diagram:    "network       FIXME"
archimate_diagram:  "archimate     FIXME"
wireframe_diagram:  "wireframe     FIXME"

diagram_name: CNAME

// ----------------------------------------------------------------------------
//  UML diagrams
// ----------------------------------------------------------------------------

_uml_diagram:                           \
     "@startuml" diagram_name? _eol     \
     hide_empty_description?            \
     scale?                             \
     _uml_content                       \
     "@enduml"

hide_empty_description: "hide empty description" _eol

scale: "scale" INT "width"

_uml_content: ( sequence_diagram
             | usecase_diagram
             | class_diagram
             | activity_diagram
             | component_diagram
             | state_diagram
             | object_diagram
             | deployment_diagram
             | timing_diagram
             | network_diagram
             | archimate_diagram )

arrow: ARROW
ARROW: /-[^>]*>/

// ----------------------------------------------------------------------------
//  State Diagram
// ----------------------------------------------------------------------------
state_diagram: ( transition
               | state
               | state_string
               | note
               | link_note
               | separator
               | skinparam )*

skinparam: "skinparam"i (_skinparamparam | skinparam_block )

_skinparamparam: start_color
               | end_color
               | background_color
               | border_color
               | font_name

skinparam_block: "state"i "{" _eol _skinparamparam+ "}" _eol

start_color:        "StartColor"i       /\w+/     _eol
end_color:          "EndColor"i         /\w+/     _eol
background_color:   "BackgroundColor"i  /\w+/     _eol
border_color:       "BorderColor"i      /\w+/     _eol
font_name:          "FontName"i         /\w+/     _eol
// AttributeFontColor blue
// AttributeFontName serif
// AttributeFontSize  9
// AttributeFontStyle italic
// FontColor red
// FontSize 15
// FontStyle bold


state: "state"i (state_long_name "as")? state_name inline_color? (string | state_block | "<<" state_type ">>")? _eol

inline_color: /#\w+/

state_block: "{" _eol state_diagram "}"

state_string: state_name string

transition: departure_state arrow arrival_state ("<<" state_type ">>")? string?  _eol

departure_state: STATE_NAME
arrival_state: STATE_NAME "[H*]"?

state_name: STATE_NAME
string: ":" FREE_TEXT

state_type: STATE_TYPE
STATE_TYPE:              \
     "choice"            \
     | "end"             \
     | "entryPoint"      \
     | "exitPoint"       \
     | "expansionInput"  \
     | "expansionOutput" \
     | "fork"            \
     | "inputPin"        \
     | "join"            \
     | "outputPin"       \
     | "sdlreceive"

state_long_name: QUOTED_STRING

note: "note"i note_position state_name note_text _eol
link_note: "note"i "on"i "link"i note_text
note_position: side "of"i

note_text: ":" FREE_TEXT
         | /\n(\s+.+\n)+/ "end"i "note"i     // TODO: Fix this

separator: separator_horizontal | separator_vertical
separator_horizontal: "--"
separator_vertical: "||"

STATE_NAME: "[*]" | "[H]" | CNAME

_event: CNAME | time_event
?time_event: ABSOLUTE_TIME_EVENT | RELATIVE_TIME_EVENT
ABSOLUTE_TIME_EVENT: "at" ":" FREE_TEXT
RELATIVE_TIME_EVENT: "after" ":" FREE_TEXT

GUARD: CNAME

FREE_TEXT: /[^ \t\n][^\n]*[^ \t\n]?/
QUOTED_STRING: /"[^"]*"/

?side: "left"i -> left
     | "right"i -> right

%import common.CNAME
%import common.INT
