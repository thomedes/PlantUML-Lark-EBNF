start: "@startuml" CNAME? "\n" state_diagram "@enduml"

WS: " " | "\t"
%ignore WS

//-----------------------------------------------------------------------------
// State Diagram
//-----------------------------------------------------------------------------
state_diagram: ( state_block | transition | state_action | note | "\n")*


transition: STATE _ARROW STATE (":" event? GUARD? action?)? "\n"
_ARROW: "->" | "-->" | "-left->" | "-right->" | "-up->" | "-down->"

?state_action: state_entry | state_do | state_exit
state_entry: STATE ":" "**entry**" action "\n"
state_do: STATE ":" "**idle**" action "\n"
state_exit: STATE ":" "**exit**" action "\n"

note: "note" side "of" STATE "\n" /.+/ "\n" "end" "note" "\n"

STATE: "[*]" | CNAME
event: CNAME | time_event
?time_event: ABSOLUTE_TIME_EVENT | RELATIVE_TIME_EVENT
ABSOLUTE_TIME_EVENT: "at" ":" FREE_TEXT
RELATIVE_TIME_EVENT: "after" ":" FREE_TEXT

GUARD: "[" FREE_TEXT "]"
action: "/" FREE_TEXT

state_block: "state" STATE "{" "\n" state_diagram "}" "\n"

FREE_TEXT: /.+/
?side: "left" -> left
    | "right" -> right

%import common.CNAME
